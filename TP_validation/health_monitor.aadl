--
-- Description de l'architecture d'un moniteur médical
--
-- Ce moniteur dispose de trois capteurs (SO2, ECG et PA), d'un affichage
-- et d'une fonction alarme qui se déclenche lorsque l'un des paramètres
-- descend en dessous d'un seuil déterminé
--

-----------------------
-- Composants matériels
-----------------------

-- Bus

-- Les capteurs sont sur bus CAN
bus CAN
end CAN;

-- L'afficheur est sur bus LVDS
bus LVDS
end LVDS;


-- Capteurs

-- Capteur saturation O2
device SO2_sensor
  features
    SO2 : out data port;
    iobus : requires bus access CAN;
end SO2_sensor;

device implementation SO2_sensor.impl
end SO2_sensor.impl;

-- Capteur électrocardiogramme
device ECG_sensor
  features
    ECG : out data port;
    iobus : requires bus access CAN;
end ECG_sensor;

device implementation ECG_sensor.impl
end ECG_sensor.impl;

-- Capteur Pression artérielle
device PA_sensor
  features
    PA : out data port;
    iobus : requires bus access CAN;
end PA_sensor;

device implementation PA_sensor.impl
end PA_sensor.impl;

-- Autres périphériques
device LCD_display
  features
    fb : in data port;					-- framebuffer
    lvds : requires bus access LVDS;	-- lcd bus
end LCD_display;
  
device Alarm
  features
    trigger : in event data port;
    iobus : requires bus access CAN;
end Alarm;

-- Processeur
processor armadeus
  features
    iobus : requires bus access CAN;
    lvds : requires bus access LVDS;
end armadeus;

processor implementation armadeus.impl
	properties 
		Scheduling_Protocol => POSIX_1003_HIGHEST_PRIORITY_FIRST_PROTOCOL;
		Cheddar_Properties::Preemptive_Scheduler => True ;
end armadeus.impl;

processor armadeus2
  features
    iobus : requires bus access CAN;
    lvds : requires bus access LVDS;
end armadeus2;

processor implementation armadeus2.impl
	properties 
		Scheduling_Protocol => POSIX_1003_HIGHEST_PRIORITY_FIRST_PROTOCOL;
		Cheddar_Properties::Preemptive_Scheduler => True ;
end armadeus2.impl;

-----------------------
-- Composants logiciels
-----------------------
thread read_sensor
  features
    raw : in data port;
    cooked : out data port;
end read_sensor;

thread implementation read_sensor.PA
	properties
		Dispatch_Protocol => Periodic ;
		Period => 5 ms;
		Compute_Execution_Time => 1 ms .. 1 ms;
		Deadline => 5 ms;
		Cheddar_Properties::Dispatch_Absolute_Time => 0 ms;
		Cheddar_Properties::Fixed_Priority => 253;	
end read_sensor.PA;

thread implementation read_sensor.ECG
	properties
		Dispatch_Protocol => Periodic ;
		Period => 20 ms;
		Compute_Execution_Time => 2 ms .. 2 ms; --4 ms .. 4 ms
		Deadline => 20 ms;
		Cheddar_Properties::Dispatch_Absolute_Time => 0 ms;
		Cheddar_Properties::Fixed_Priority => 251;
end read_sensor.ECG;

thread implementation read_sensor.SO2
	properties
		Dispatch_Protocol => Periodic ;
		Period => 5 ms;
		Compute_Execution_Time => 2 ms .. 2 ms;
		Deadline => 5 ms;
		Cheddar_Properties::Dispatch_Absolute_Time => 0 ms;
		Cheddar_Properties::Fixed_Priority => 254;
end read_sensor.SO2;

thread check_bounds
  features
    PA : in data port;
    ECG : in data port;
    SO2 : in data port;
    alarm : out event data port;
end check_bounds;

thread implementation check_bounds.impl
	properties
		Dispatch_Protocol => Periodic ;
		Period => 6 ms;
		Compute_Execution_Time => 4 ms .. 4 ms; --2 ms .. 2 ms;
		Deadline => 6 ms;
		Cheddar_Properties::Dispatch_Absolute_Time => 0 ms;
		Cheddar_Properties::Fixed_Priority => 252;
end check_bounds.impl;

thread display
  features
    PA : in data port;
    ECG : in data port;
    SO2 : in data port;
    fb : out data port;
end display;

thread implementation display.impl
	properties
		Dispatch_Protocol => Periodic ;
		Period => 3 ms;
		Compute_Execution_Time => 2 ms .. 2 ms;
		Deadline => 3 ms;
		Cheddar_Properties::Dispatch_Absolute_Time => 0 ms;
		Cheddar_Properties::Fixed_Priority => 255;
end display.impl;

process health_monitoring
  features
    PA : in data port;
    ECG : in data port;
    SO2 : in data port;
    fb : out data port;
    alarm : out event data port;
end health_monitoring;


process implementation health_monitoring.impl
  subcomponents
    getSO2 : thread read_sensor.SO2;
    dpy : thread display.impl;
end health_monitoring.impl;


process health_monitoring2
  features
    PA : in data port;
    --ECG : in data port;
    SO2 : in data port;
    fb : out data port;
    alarm : out event data port;
end health_monitoring2;


process implementation health_monitoring2.impl
  subcomponents
    getECG : thread read_sensor.ECG;
    check : thread check_bounds.impl;
    getPA : thread read_sensor.PA;
end health_monitoring2.impl;

------------------
-- système complet
------------------
system health_monitor
end health_monitor;

system implementation health_monitor.impl
  subcomponents
    -- Les bus
    can : bus CAN;
    lvds : bus LVDS;
    
    -- Les processeurs
    cpu_a : processor armadeus.impl;
    cpu_b : processor armadeus2.impl;
    
    -- Les capteurs
    SO2_sense : device SO2_sensor;
    PA_sense : device PA_sensor;
    ECG_sense : device ECG_sensor;
    
    -- Les périphériques de sortie
    lcd : device LCD_display;
    alarm : device Alarm;
    
    -- L'application
    appli : process health_monitoring.impl;
    appli2 : process health_monitoring2.impl;
    
    
  connections
    -- connexions de bus
    bus access can -> SO2_sense.iobus;
    bus access can -> PA_sense.iobus;
    bus access can -> ECG_sense.iobus;
    bus access can -> alarm.iobus;
    bus access can -> cpu_a.iobus;
       
    bus access lvds -> lcd.lvds;
    bus access lvds -> cpu_a.lvds;
  
    -- connexions de ports
    so2_conn : data port SO2_sense.SO2 -> appli.SO2;
    pa_conn : data port PA_sense.PA -> appli.PA;
    ecg_conn : data port ECG_sense.ECG -> appli.ECG;
    fb_conn : data port appli.fb -> lcd.fb;
    alarm_conn : event data port appli.alarm -> alarm.trigger;
    
  properties
    -- Allocation logiciel - plateforme d'exécution
    Actual_Processor_Binding => reference cpu_a applies to appli;
    Actual_Processor_Binding => reference cpu_b applies to appli2;
    
    Actual_Connection_Binding => reference can applies to so2_conn;
    Actual_Connection_Binding => reference can applies to pa_conn;
    Actual_Connection_Binding => reference can applies to ecg_conn;
    Actual_Connection_Binding => reference can applies to alarm_conn;
    Actual_Connection_Binding => reference lvds applies to fb_conn;
end health_monitor.impl;

    
    
    










